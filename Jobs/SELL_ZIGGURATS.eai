#IFDEF GLOBAL
#ELSE
function SelectByZiggurat takes group rg returns group
  local unit u = null
  local integer i = 0
  local integer id = 0
  local group g = rg
  set rg = CreateGroup()
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if UnitAlive(u) and not IsUnitHidden(u) then
      set id = GetUnitTypeId(u)
      loop
        exitwhen i >= ziggarray
        if id == racial_zigg[i] then
          call GroupAddUnit(rg, u)
        endif
        set i = i + 1
      endloop
      set i = 0
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  return rg
endfunction

function CheckSellZiggurat takes unit u, group cg returns nothing
  local group g = CreateGroup()
  local unit z = null
  local integer i = 0
  call GroupEnumUnitsInRange(g, GetUnitX(u), GetUnitY(u), race_ziggurat_keep_distance, null)
  call GroupRemoveUnit(g, u)
  loop
    set z = FirstOfGroup(g)
    exitwhen z == null or i > 0
    if GetOwningPlayer(z) == ai_player and IsUnitType(z, UNIT_TYPE_STRUCTURE) and UnitAlive(z) and not IsUnitHidden(z) and not IsUnitInGroup(z, cg) then
      set i = i + 1
    endif
    call GroupRemoveUnit(g, z)
  endloop
  if i < 1 then  // around no other buildings
    call Unsummon(u)
  endif
  call DestroyGroup(g)
  set g = null
  set z = null
endfunction

function CheckSellStructure takes group g returns nothing
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, false)
  set g = SelectByIdOr(g, old_id[racial_lumber], old_id[racial_expansion], false)
  set g = SelectByIdOr(g, old_id[racial_shop], old_id[racial_altar], false)
  set g = SelectByAlive(g, true)
  set g = SelectByHidden(g, false)
  call Unsummon(FirstOfGroup(g))
endfunction

function SellZigguratJob takes nothing returns nothing
  local group g = CreateGroup()
  local group cg = null
  local unit u = null
  call DisplayToAllJobDebug("SELL_ZIGGURATS JOB START")
  if not towerrush and GetGold() < 2000 then
    if GetGoldOwned() + GetGold() < 800 then  // if insufficient resources, buy more buildings , else ensure there is sufficient food available
      call GroupEnumUnitsOfPlayer(g, ai_player, null)
      call CheckSellStructure(g)
    elseif TownCountDone(racial_farm) * GetFoodMade(old_id[racial_farm]) + 20 > Min(FoodUsed(), ver_food_limit) then
      call GroupEnumUnitsOfPlayer(g, ai_player, null)
      set g = SelectByZiggurat(g)
      set cg = CopyGroup(g, cg)
      loop
        set u = FirstOfGroup(g)
        exitwhen u == null
        call CheckSellZiggurat(u, cg)
        call GroupRemoveUnit(g, u)
      endloop
      call DestroyGroup(cg)
      set cg = null
    endif
  endif
  call DestroyGroup(g)
  set g = null
  if not pathing_done then
    call TQAddJob(30, SELL_ZIGGURATS, 0)
  endif
endfunction
#ENDIF